#include <SoftwareSerial.h>
#define DEBUG true
 
SoftwareSerial esp8266(2,3); //RX는 2번 핀, TX는 3번 핀
const int LED=13;
 
void setup() {
  // put your setup code here, to run once:
   Serial.begin(9600);
   esp8266.begin(9600); //esp의 보드레이트
 
   pinMode(LED, OUTPUT); //LED가 꽂힌 핀을 output으로 설정
   digitalWrite(LED, LOW); //LED를 끈 상태로 초기화
 
  /*AT Command 이용*/
  sendData("AT+RST\r\n", 2000, DEBUG); //reset module
  sendData("AT+CWMODE=3\r\n", 1000, DEBUG); //dual mode로 설정
  sendData("AT+CWJAP=\"SSID\",\"PASSWORD\"\r\n", 5000, DEBUG); //사용할 공유기 설정
  sendData("AT+CIPMUX=1\r\n", 1000, DEBUG); //multiple connections 설정
  sendData("AT+CIPSERVER=1,80\r\n", 1000, DEBUG); //포트 번호를 80번으로 설정
}
 
void loop() {
  // put your main code here, to run repeatedly:
  if(esp8266.available()){ //esp8266에 웹에서 메시지가 왔는지 파악
    if(esp8266.find("+IPD,")){ //data를 받아옴
      delay(1000); //시리얼 버퍼에서 보내져온 데이터가 다 찰 때까지 대기
      int connectionID=esp8266.read()-48; //connection ID를 알아내고 아스키코드값이므로 48을 빼줌
      esp8266.find("pin=");
      int pinNumber=(esp8266.read()-48)*10; //get the first number
      pinNumber+=(esp8266.read()-48); //get the second number
      digitalWrite(pinNumber, !digitalRead(pinNumber)); //LED toggle
      /*연결을 끊음*/
      String closeCommand="AT+CIPCLOSE=";
      closeCommand+=connectionID;
      closeCommand+="\r\n";
      sendData(closeCommand, 1000, DEBUG);
    }
  }
}
 
/*ESP8266의 정보를 알아내고 설정하기 위한 함수 선언*/
String sendData(String command, const int timeout, boolean debug){
  String response = "";
  esp8266.print(command); //command를 ESP8266에 보냄
  long int time=millis();
  
  while((time+timeout)>millis()){
    while(esp8266.available()){
      /*esp가 가진 데이터를 시리얼 모니터에 출력하기 위함*/
      char c=esp8266.read(); //다음 문자를 읽어옴
      response+=c;
    }
  }
  if(debug){
    Serial.print(response);
  }
 
  return response;
}